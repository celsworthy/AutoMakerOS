#version 150

uniform int firstSelectedLine;
uniform int lastSelectedLine;
uniform int topVisibleLine;
uniform int bottomVisibleLine;
uniform int showFlags; // 2 = show colour as type. 4 = show colour as data. 8 = show only selected
uniform int showTools; // Bit n(in range 0-15) 0 = hide tool n, 1 = show tool n.
uniform int showTypes; // Bit n(in range 0-15) 0 = hide type n, 1 = show type n.
uniform vec4 toolColours[16];
uniform vec4 typeColours[16];
uniform vec3 selectColour;

in vec3 position;
in vec4 direction;
in vec4 normal;
in vec3 colour;
in vec4 dimensions; // x = length, y = width, z = thickness, w = 1.0
in vec4 attributes; // x = type, y = layer, z = line, w = tool
in vec4 angles; // x = a, y = b, z = 0, w = 0

out vec4 vDirection;
out vec4 vDimensions;
out vec4 vNormal;
out vec4 vColour;
out vec4 vAngles;

void main(void) {
    gl_Position = vec4(position, 1.0);
	vDimensions = dimensions;
	vDirection = direction;
	vNormal = normal;
	vAngles = angles;
	
	int tool = int(attributes.w + 0.5);

	int toolFlag = 1 << tool;
		
	if (((showTools & toolFlag) == toolFlag) &&
		(attributes.z >= bottomVisibleLine && attributes.z <= topVisibleLine)) {
            if ((showFlags & 2) == 2) { // Colour as type
                    int type = int(attributes.x + 0.5);
                    int typeFlag = 1 << type;
                    if ((showTypes & typeFlag) == typeFlag)
                            vColour = vec4(typeColours[type].rgb, 1.0);
                    else
                            vColour = vec4(0.0, 0.0, 0.0, 0.0);
            }
            else if ((showFlags & 4) == 4) // Colour as data.
                    vColour = vec4(colour, 1.0);
            else
                    vColour = vec4(toolColours[tool].rgb, 1.0);

            if(vColour.a >= 0.001 && (vColour.r + vColour.g + vColour.b) < 0.3) {
                vColour.rgb += 0.2; // Make very dark items lighter
            }

            if (firstSelectedLine != lastSelectedLine &&
			(attributes.z < firstSelectedLine || attributes.z >= lastSelectedLine)) {

			if ((showFlags & 8) == 8) // Show only selected.
				vColour = vec4(0.0, 0.0, 0.0, 0.0);
			else
				vColour.rgb *= 0.6; // Make unselected things darker.
            }
	}
	else
		vColour = vec4(0.0, 0.0, 0.0, 0.0);
}
